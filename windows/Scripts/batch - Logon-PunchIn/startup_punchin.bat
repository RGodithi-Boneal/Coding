@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION

REM		Created by Matt Cavallo <mcavallo@boneal.com>, 2018-11-28

SET BGINFO_UPDATER=C:\ISO\UpdateBGInfo.lnk
SET SAGE_PUNCHIN_BNL=C:\ISO\BNL-Time.lnk
SET SAGE_PROCESS_NAME=pvxwin32.exe

SET RUNTIME_HOSTNAME=%COMPUTERNAME%
SET TARGET_HOSTNAME=%COMPUTERNAME%

SET RUNTIME_USERDOMAIN=%USERDOMAIN%
SET RUNTIME_USERNAME=%USERNAME%

SET TARGET_USERNAME=ABCDEFGHIJKLMNOPQRSTUVWXYZ
SET TARGET_USERDOMAIN=ABCDEFGHIJKLMNOPQRSTUVWXYZ

SET SECONDS_TO_CLOSE_SAGE_PUNCHIN_AFTER=180

REM	Optionally, target specific domain\username (instead of self)
IF NOT "%1"=="" (
IF NOT "%2"=="" (
SET TARGET_USERDOMAIN=%1
SET TARGET_USERNAME=%2
)
)

ECHO.
ECHO  RUNTIME_USERDOMAIN: %RUNTIME_USERDOMAIN%
ECHO  RUNTIME_USERNAME:   %RUNTIME_USERNAME%
ECHO.
ECHO  TARGET_USERDOMAIN:  %TARGET_USERDOMAIN%
ECHO  TARGET_USERNAME:    %TARGET_USERNAME%
ECHO.

REM	 Find Session-ID tied to [target-user]
SET USER_SESSION_ID=NOTFOUND
FOR /F "tokens=3-4" %%a IN ('QUERY SESSION %TARGET_USERNAME%') DO (
	@IF "%%b"=="Active" (
		SET USER_SESSION_ID=%%a
	)
)

REM	 Determine if [target-user] is logged-in or not
IF NOT "%USER_SESSION_ID%"=="NOTFOUND" (

	IF NOT "%3"=="" (
		ECHO %1| clip
	)
	
	IF EXIST %BGINFO_UPDATER% (
		START %BGINFO_UPDATER%
	)

	IF EXIST %SAGE_PUNCHIN_BNL% (
	
		START %SAGE_PUNCHIN_BNL%
	
		TIMEOUT /T %SECONDS_TO_CLOSE_SAGE_PUNCHIN_AFTER%
	
		REM	 Safely end SAGE sessions which were started by [target-user]
		ECHO "Killing Images equal to '%SAGE_PROCESS_NAME%' for User: '%TARGET_USERDOMAIN%\%TARGET_USERNAME%'"
		ECHO.
		TASKKILL /FI "USERNAME eq %TARGET_USERDOMAIN%\%TARGET_USERNAME%" /FI "IMAGENAME eq %SAGE_PROCESS_NAME%"
	
	)

)

EXIT

REM Logs 2018-11-28 - Batch file created
REM Logs 2018-12-12 - Updated to separate RUNTIME from TARGET domain & user